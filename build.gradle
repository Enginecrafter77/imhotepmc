buildscript {
    repositories {
        maven { url = 'https://files.minecraftforge.net/maven' }
        maven { url = 'https://plugins.gradle.org/m2' }
		maven { url = 'https://maven.enginecrafter77.dev/general' }
        mavenCentral()
    }
    dependencies {
        classpath 'gradle.plugin.fr.brouillard.oss.gradle:gradle-jgitver-plugin:0.10.0-rc03'
        classpath 'net.minecraftforge.gradle:ForgeGradle:2.3-SNAPSHOT'
        classpath 'gradle.plugin.com.matthewprenger:CurseGradle:1.4.0'
		classpath 'dev.enginecrafter77.githubrelease:githubrelease:0.1.1'
    }
}

apply plugin: 'dev.enginecrafter77.githubrelease'
apply plugin: 'fr.brouillard.oss.gradle.jgitver'
apply plugin: 'net.minecraftforge.gradle.forge'
apply plugin: 'com.matthewprenger.cursegradle'

group = "dev.enginecrafter77.imhotepmc"
archivesBaseName = "ImhotepMC"

// Need this here so eclipse task generates correctly.
sourceCompatibility = targetCompatibility = compileJava.sourceCompatibility = compileJava.targetCompatibility = '1.8'

minecraft {
    version = "1.12.2-14.23.5.2847"
    mappings = "stable_39"
    runDir = "run"

    clientJvmArgs = ["-ea"]
    serverJvmArgs = ["-ea"]
    clientRunArgs = ["--username=${System.getenv("MINECRAFT_USERNAME")}"]
}

repositories {
    maven { url = 'https://modmaven.dev' }
    maven { url = "https://cursemaven.com" }
}

test {
    useJUnitPlatform()
}

dependencies {
    compile 'org.apache.logging.log4j:log4j-jcl:2.8.1'
    compile 'org.apache.commons:commons-lang3:3.5'
    compile 'li.cil.oc:OpenComputers:MC1.12.2-1.7.5.+:api'

    runtime "codechicken:CodeChickenLib:1.12.2-3.2.3.+:universal"
    runtime "cofh:CoFHCore:1.12.2-4.6.3.+:universal"
    runtime "cofh:CoFHWorld:1.12.2-1.3.1.+:universal"
    runtime "cofh:ThermalFoundation:1.12.2-2.6.0.+:universal"
    runtime "cofh:ThermalExpansion:1.12.2-5.5.4.+:universal"
    runtime "cofh:ThermalDynamics:1.12.2-2.5.5.+:universal"
    runtime "cofh:RedstoneFlux:1.12-2.1.0.7:universal"
    runtime "mezz.jei:jei_1.12.2:4.9.2.196"
    runtime "curse.maven:Hwyla-253449:2568751"

    testImplementation 'org.junit.jupiter:junit-jupiter-api:5.8.1'
    testRuntimeOnly 'org.junit.jupiter:junit-jupiter-engine:5.8.1'
}

jgitver {
    autoIncrementPatch false
    nonQualifierBranches "main"
}

jar {
    manifest {
        attributes([
                "Specification-Title": project.name,
                "Specification-Vendor": "Enginecrafter77",
                "Specification-Version": "1", // We are version 1 of ourselves
                "Implementation-Title": project.name,
                "Implementation-Version": project.version,
                "Implementation-Vendor": "Enginecrafter77",
                "Implementation-Timestamp": new Date().format("yyyy-MM-dd'T'HH:mm:ssZ")
        ])
    }
}

processResources {
    // this will ensure that this task is redone when the versions change.
    inputs.property "mcversion", project.minecraft.version
    inputs.property "version", project.version.toString()

    // replace stuff in mcmod.info, nothing else
    from(sourceSets.main.resources.srcDirs) {
        include 'mcmod.info'

        // replace version and mcversion
        expand 'version': project.version, 'mcversion': project.minecraft.version
    }

    // Move ATs to meta inf directory
    rename '(.+_at.cfg)', 'META-INF/$1'
}

curseforge {
    apiKey = System.getenv("CURSEFORGE_APIKEY") ?: '#invalid-key#'
    project {
        id = project.properties['curseforge.project_id'] ?: '#invalid-id#'
        releaseType = System.getenv("CURSEFORGE_RELEASE_TYPE") ?: 'release'
        changelogType = 'markdown'
        changelog = "For changelog, see the github repository."

        mainArtifact(jar) {
            displayName = "${project.archivesBaseName}-${project.minecraft.version}-${project.version}.jar"
        }
    }
    options {
        debug System.getenv("DRY_RUN") != null
    }
}

github {
	repository = project.properties['github.repository']
	token = System.getenv("GITHUB_TOKEN")
	artifacts {
		fromJar(jar) {
			name = "${archivesBaseName}-${minecraft.version}-${version}.jar"
		}
	}
	release {
		useLatestTag()
	}
}
